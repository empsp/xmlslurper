/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'mturski' at '10/28/16 4:43 PM' with Gradle 2.14
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.14/userguide/tutorial_java_projects.html
 */

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'org.xs4j'
archivesBaseName = 'xmlslurper'
version = '3.0.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Marek Turski"
            email "turski.marek@gmail.com"
        }
    }
}


publishing {
    publications {
        XMLSlurperPublication(MavenPublication) {
            from components.java
            groupId 'org.xs4j'
            artifactId 'xmlslurper'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An attempt to port parsing capabilities offered by Groovy XMLSlurper into the Java world. The following is not planned to be accurate projection, instead the most useful functions will be implemented.')
                root.appendNode('name', 'XMLSlurper for Java')
                root.appendNode('url', 'https://github.com/empsp/xmlslurper')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'org.xs4j:xmlslurper'
        userOrg = 'empsp'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/empsp/xmlslurper.git'
        version {
            name = project.version
        }
        publications = ['XMLSlurperPublication']

        githubRepo = 'empsp/xmlslurper'
        githubReleaseNotesFile = 'README.md'
    }
}
