buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
}


group = 'org.xs4j'
archivesBaseName = 'xmlslurper'
version = '3.0.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

def pomConfig = {
    scm {
        connection 'scm:git:git://github.com/empsp/xmlslurper.git'
        developerConnection 'scm:git:git://github.com/empsp/xmlslurper.git'
        url 'http://xs4j.org'
    }

    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    developers {
        developer {
            name "Marek Turski"
            email "turski.marek@gmail.com"
        }
    }
}


publishing {
    publications {
        XMLSlurper(MavenPublication) {
            from components.java
            groupId 'org.xs4j'
            artifactId 'xmlslurper'
            version project.version
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'XMLSlurper for Java')
                root.appendNode('description', 'An attempt to port parsing capabilities offered by Groovy XMLSlurper into the Java world. The following is not planned to be accurate projection, instead the most useful functions will be implemented.')
                root.appendNode('url', 'http://xs4j.org')
                root.appendNode('packaging', 'jar')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('bintray.user')
    key = System.getenv('bintray.key')
    pkg {
        repo = 'maven'
        name = 'org.xs4j:xmlslurper'
        licenses = ['MIT']
        userOrg = System.getenv('bintray.user')
        vcsUrl = 'https://github.com/empsp/xmlslurper.git'
        publications = ['XMLSlurper']
        publicDownloadNumbers = true

        version {
            name = project.version
            gpg {
                sign = true
                passphrase = System.getenv('gpg.passphrase')
            }
        }
    }
}
